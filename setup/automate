#!/usr/bin/env zsh

set -e

# parse options {{{
usage() {
    cat <<- 'EOU'
		Usage: automate [options] [base directory]

		Options
		  -b, --branch BRANCH   git branch
		  -r, --repo REPO       clone from REPO
		  -h, --help            print this help message
	EOU
}


_OPTIONS=(
    branch:
    help
    repo:
)

if ! _TEMP=$(POSIXLY_CORRECT=true getopt -q -o b:hr: --long ${(j:,:)_OPTIONS} -- "$@"); then
    # exit if error
    usage
    exit 1
fi

eval set -- $_TEMP

_BRANCH=
_GITHUB_REPO='https://github.com/nuimk/nmk.git'
_REPO=$_GITHUB_REPO

while true; do
    case $1 in
        -b | --branch ) _BRANCH=$2; shift 2 ;;
        -r | --repo ) _REPO=$2; shift 2 ;;
        -h | --help ) usage; exit 0 ;;
        -- ) shift; break ;;
    esac
done
# }}}

_NMK_DIR=${${1:-$HOME/.nmk}:A}

if [[ -n $_BRANCH ]]; then
    git clone --quiet --branch $_BRANCH $_REPO $_NMK_DIR
else
    git clone --quiet $_REPO $_NMK_DIR
fi
cd $_NMK_DIR
git submodule init
git submodule --quiet update --recursive
git remote set-url origin $_GITHUB_REPO
NMK_DIR=$_NMK_DIR ./vim/update-plugins

